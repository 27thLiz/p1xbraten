mastermenushowbots = 0
mastermenushowself = 0

newgui master [
    local mm teamtable teamname teamnamecolor teamplayers im teams numteams player specs
    guinoautotab [ guistayopen [
        guilist [ // horizontal
            if (ismaster (getclientnum)) [
                guibutton "relinquish master" "setmaster 0"
            ] [
                guibutton "claim master" "setmaster 1"
            ]
            guistrut 3
            guispring
            guitext "mastermode:" 0
            guistrut 1
            mm = (getmastermode)
            guilist [ // vertical
                guiradio "open" mm 0 "mastermode 0"
                guiradio "veto" mm 1 "mastermode 1"
            ]
            guistrut 1
            guilist [ // vertical
                guiradio "locked" mm 2 "mastermode 2"
                guiradio "private" mm 3 "mastermode 3"
            ]
            guispring
            guistrut 3
            guilist [
                guicheckbox "show bots" mastermenushowbots
                guicheckbox "show self" mastermenushowself
            ]
        ]

        teamtable = [
            teamname = $arg1
            if $teamname [
                teamnamecolor = (? (=s $teamname "spectators") "^f2" (? (=s $teamname $getteam) "^f1" "^f3"))
                guitext (concatword $teamnamecolor $teamname ":") 0
            ]
            teamplayers = $arg2
            guilist [ // horizontal
                guilist [ // vertical
                    guitext "^f4name" 0
                    looplist cn $teamplayers [
                        guitext (concatword "^f7" (getclientname $cn)) 0
                    ]
                ]
                guistrut 2
                guilist [ // vertical
                    guialign 1 [ guitext "^f4cn" 0 ]
                    looplist cn $teamplayers [
                        guialign 1 [ guitext (concatword (? (isai @cn) "^f5" "^f7") @cn) 0 ]
                    ]
                ]
                if (|| (=s $teamname "evil") (=s $teamname "good")) [
                    guistrut 2
                    guilist [ // vertical
                        guitext "^f4team" 0
                        looplist cn $teamplayers [
                            guibutton "switch" [setteam @cn @(? (=s (getclientteam $cn) "good") "evil" "good")] 0
                        ]
                    ]
                ]
                guistrut 2
                guilist [ // vertical
                    guialign 0 [ guitext "^f4master" 0 ]
                    looplist cn $teamplayers [
                        if (! (isai $cn)) [
                            guialign 0 [
                                im = (ismaster @@cn)
                                guibutton "" [setmaster (mod (+ (ismaster @@@cn) 1) 2) @@@cn] (? $im "radio_on" "radio_off")
                            ]
                        ]
                    ]
                ]
                guistrut 2
                guilist [ // vertical
                    guialign 0 [ guitext (concatword "^f4" (? (=s @teamname "spectators") "unspec" "spec")) 0 ]
                    looplist cn $teamplayers [
                        if (! (isai $cn)) [
                            guialign 0 [ guibutton "" [spectator (? (=s @@@teamname "spectators") 0 1) @@@cn] "spectator" ]
                        ]
                    ]
                ]
                guistrut 2
                guilist [ // vertical
                    guialign 0 [ guitext "^f4kick" 0 ]
                    looplist cn $teamplayers [
                        guialign 0 [ guibutton "" [if (isai @@cn) [delbot] [kick @@@cn]] "exit" ]
                    ]
                ]
            ]
        ]

        guialign 0 [ // horizontal
            guilist [ // vertical

                teams = (listteams $mastermenushowself $mastermenushowbots 0)
                numteams = (listlen $teams)
                if $numteams [
                    loop i (div $numteams 2) [
                        guistrut 1
                        guilist [ // horizontal
                            guilist [ // vertical
                                teamname = (at $teams (* $i 2))
                                teamtable $teamname (listclients $mastermenushowself $mastermenushowbots 0 $teamname)
                            ]
                            guistrut 4
                            guilist [ // vertical
                                teamname = (at $teams (+ (* $i 2) 1))
                                teamtable $teamname (listclients $mastermenushowself $mastermenushowbots 0 $teamname)

                            ]
                        ]
                    ]
                    if (mod $numteams 2) [
                        guistrut 1
                        teamname = (at $teams (- $numteams 1))
                        teamtable $teamname (listclients $mastermenushowself $mastermenushowbots 0 $teamname)
                    ]
                ] [
                    guistrut 1
                    players = (listclients $mastermenushowself $mastermenushowbots 0)
                    if (listlen $players) [
                        teamtable "" $players
                    ] [
                        guialign 0 [ guitext "^f4change the filters above to see players" ]
                    ]
                ]

                specs = (listfilter cn (listclients $mastermenushowself $mastermenushowbots 1) [ isspectator $cn ])
                if (listlen $specs) [
                    guistrut 1
                    teamtable "spectators" $specs
                ]
            ]
        ]
    ] ]
]
