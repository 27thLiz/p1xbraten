p1xbratengamehud = [
	local playerstates hud

	playerstates = [
		local playernamecolor playernamelist teamnamecolor teamplayernamelist teamnames playercns

		// CN -> yellow (if followed by player1), white (alive), or gray (dead)
		playernamecolor = [ ? (= $arg1 $getfollow) "^f2" (? (isdead $arg1) "^f4" "^f7") ]

		// list of CNs -> listing of colored names
		playernamelist = [
			looplistconcatword cn $arg1 [
				concatword "^n^fs" (playernamecolor $cn) (getclientname $cn) "^fr"
			]
		]

		// team name -> blue if same team as player1, red otherwise
		teamnamecolor = [ ? (=s $arg1 $getteam) "^f1" "^f3" ]

		// team name, team players -> listing of colored team name and colored player names
		teamplayernamelist = [
			concatword "^fs" (teamnamecolor $arg1) $arg1 ":^fr" (playernamelist $arg2)
		]

		teamnames = (listteams 1 1 0)
		if (listlen $teamnames) [
			looplistconcatword teamname $teamnames [
				playercns = (listclients 1 1 0 $teamname)
				if (listlen $playercns) [
					concatword (teamplayernamelist $teamname $playercns) "^n^n"
				]
			]
		] [
			playercns = (listclients 1 1 0)
			if (listlen $playercns) [
				concatword "^fs^f7players:^fr" (playernamelist $playercns) "^n^n"
			]
		]
	]

	if (isspectator $getclientnum) [
		hud = (concatword (playerstates) "^fs^f2SPECTATOR^fr^n")
	]

	if (getalias gamehud) [
		hud = (concatword $hud "^n" (gamehud))
	]

	result $hud
]
