p1xbratengamehud = [
	local playerstates
	playerstates = [
		local playerlist teamlist teams players
		playerlist = [
			local players playernamecolor
			players = $arg1
				looplistconcatword cn $players [
					playernamecolor = (? (= $getfollow $cn) "^f2" (? (isdead $cn) "^f4" "^f7"))
					concatword "^n^fs" $playernamecolor (getclientname $cn) "^fr"
				]
		]
		teamlist = [
			local teamname teamplayers teamnamecolor
			teamname = $arg1
			teamplayers = $arg2
			if $teamname [
				teamnamecolor = (? (=s $teamname "spectators") "^f2" (? (=s $teamname $getteam) "^f1" "^f3"))
			]
			concatword "^fs" $teamnamecolor $teamname ":^fr" (playerlist $teamplayers)
		]
		teams = (listteams 1 1 0)
		if (listlen $teams) [
			looplistconcatword teamname $teams [
				players = (listclients 1 1 0 $teamname)
				result (? (listlen $players) (concatword (teamlist $teamname $players) "^n^n") "")
			]
		] [
			players = (listclients 1 1 0)
			result (? (listlen $players) (concatword "^fs^f7players:^fr" (playerlist $players) "^n^n") "")
		]
	]

	local specgamehud
	if (isspectator $getclientnum) [
		specgamehud = (concatword (playerstates) "^fs^f2SPECTATOR^fr^n")
	]

	if (> (strlen (getalias "gamehud")) 0) [
		specgamehud = (concatword $specgamehud "^n" (gamehud))
	]

	result $specgamehud
]
