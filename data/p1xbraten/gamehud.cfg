p1xbratengamehud = [
	local playerstates hud

	playerstates = [
		local playernamelist teamplayernamelist teamnames playercns
		playernamelist = [
			looplistconcatword cn $arg1 [
				concatword "^n^fs" (? (= $getfollow $cn) "^f2" (? (isdead $cn) "^f4" "^f7")) (getclientname $cn) "^fr"
			]
		]
		teamplayernamelist = [
			concatword "^fs" (? (=s $arg1 "spectators") "^f2" (? (=s $arg1 $getteam) "^f1" "^f3")) $arg1 ":^fr" (playernamelist $arg2)
		]
		teamnames = (listteams 1 1 0)
		if (listlen $teamnames) [
			looplistconcatword teamname $teamnames [
				playercns = (listclients 1 1 0 $teamname)
				if (listlen $playercns) [
					concatword (teamplayernamelist $teamname $playercns) "^n^n"
				]
			]
		] [
			playercns = (listclients 1 1 0)
			if (listlen $playercns) [
				concatword "^fs^f7players:^fr" (playernamelist $playercns) "^n^n"
			]
		]
	]

	if (isspectator $getclientnum) [
		hud = (concatword (playerstates) "^fs^f2SPECTATOR^fr^n")
	]

	if (getalias gamehud) [
		hud = (concatword $hud "^n" (gamehud))
	]

	result $hud
]
